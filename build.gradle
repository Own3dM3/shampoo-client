plugins {
    id 'fabric-loom' version '1.10.1'
    id 'maven-publish'
    id 'idea'
}

version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
}

loom {
    accessWidenerPath = file("src/main/resources/shampoo.accesswidener")
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

tasks.register('generateBuildConfig') {
    def outputDir = layout.buildDirectory.dir('generated/sources/buildConfig/java')
    outputs.dir(outputDir)

    doLast {
        def hash = 'unknown'
        try {
            def proc = 'git rev-parse HEAD'.execute(null, project.projectDir)
            proc.waitFor()
            if (proc.exitValue() == 0) {
                hash = proc.text.trim()
            }
        } catch (ignored) {}

        def time = new Date().format("yyyy-MM-dd HH:mm", TimeZone.getTimeZone("UTC"))
        def buildNumber = System.getenv("BUILD_NUMBER") ?: new Date().format("yyMMdd")
        def identifier = "dev"

        def pkgDir = outputDir.get().asFile.toPath().resolve('uid/infinity/shampoo').toFile()
        pkgDir.mkdirs()
        new File(pkgDir, "BuildConfig.java").text = """
package uid.infinity.shampoo;

public final class BuildConfig {
    public static final String HASH = "${hash}";
    public static final String VERSION = "${project.version}";
    public static final String BUILD_NUMBER = "${buildNumber}";
    public static final String BUILD_IDENTIFIER = "${identifier}";
    public static final String BUILD_TIME = "${time}";

    private BuildConfig() {}
}
"""
    }
}

sourceSets {
    main {
        java {
            srcDir(tasks.named('generateBuildConfig'))
        }
    }
}

idea {
    module {
        sourceDirs += layout.buildDirectory.dir('generated/sources/buildConfig/java').get().asFile
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {

    }
}